dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(lxdream, 0.8.1)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_CANONICAL_BUILD
AC_CANONICAL_HOST

PKG_CHECK_MODULES(PACKAGE, 
   [ gtk+-2.0 dnl
     esound dnl
     libpng ])
AC_CHECK_LIB(z, uncompress, [], [
    echo "Zlib (libz.so) could not be found, but is required."
    exit 1])
AC_CHECK_LIB(GL, glXQueryVersion, [], [
    echo "The OpenGL library (libGL.so) could not be found, but is required."
    exit 1])
AC_CHECK_HEADER([GL/gl.h], [], [
    echo "The OpenGL header files (eg GL/gl.h) could not be found, but are required."
    exit 1])

dnl Check for a supported cpu target for translation purposes
case $host_cpu in
    i386|i486|i586|i686)
	SH4_TRANSLATOR="x86"
        AC_DEFINE_UNQUOTED(SH4_TRANSLATOR,["x86"], [SH4 Translator to use (if any)] );;
    x86_64)
	SH4_TRANSLATOR="x86_64"
        AC_DEFINE_UNQUOTED(SH4_TRANSLATOR,["x86_64"], [SH4 Translator to use (if any)] );;
    *)
	echo "Warning: No translator available for $host. Building emulation core only";;
esac
AM_CONDITIONAL(BUILD_SH4X86, [test "$SH4_TRANSLATOR" = "x86" -o "$SH4_TRANSLATOR" = "x86_64"])

dnl Check for cdrom device support
AC_CHECK_HEADER([linux/cdrom.h], [HAVE_LINUX_CDROM_H=yes], [
    echo "Linux CDROM support not found, building without it."]  )
AM_CONDITIONAL(CDROM_LINUX, [test "$HAVE_LINUX_CDROM_H" = "yes"])

dnl Check for cross-compiler availability
AC_PATH_PROG(SHCC, [sh-elf-gcc])
AC_PATH_PROG(SHLD, [sh-elf-ld])
AC_PATH_PROG(SHOBJCOPY, [sh-elf-objcopy])
AC_PATH_PROG(ARMCC, [arm-elf-gcc])
AC_PATH_PROG(ARMLD, [arm-elf-ld])
AC_PATH_PROG(ARMOBJCOPY, [arm-elf-objcopy])

if test "x$SHCC" = "x" -o "x$SHLD" = "x" -o "x$SHOBJCOPY" = "x"; then
  BUILD_SH="no"
  echo "Warning: SH4 cross-compiler not found, system tests will be disabled."
else
  BUILD_SH="yes"
fi
if test "x$ARMCC" = "x" -o "x$ARMLD" = "x" -o "x$ARMOBJCOPY" = "x"; then
  BUILD_ARM="no"
  echo "Warning: ARM cross-compiler not found, AICA tests will be disabled."
else
  BUILD_ARM="yes"
fi
AM_CONDITIONAL(BUILD_SYSTEST, [test "$BUILD_SH" = "yes"])
AM_CONDITIONAL(BUILD_ARMTEST, [test "$BUILD_ARM" = "yes"])

GETTEXT_PACKAGE=lxdream
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")
AC_SUBST(PACKAGE_CFLAGS)
AC_SUBST(PACKAGE_LIBS)


dnl Add the languages which your application supports here.
ALL_LINGUAS="de pt_BR"
AM_GLIB_GNU_GETTEXT

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
test/Makefile
])

